@top Program { expression }

expression {
  ejsExpression |
  ( quoteSingle ejsExpression quoteSingle ) |
  ( quoteDouble ejsExpression quoteDouble ) |
  Text
}

ejsExpression {  OpeningTag JavascriptExpression ClosingTag}

@skip {} {
  Comment { "<%#" (CommentContent | commentNewLine)* commentEnd }
}

@skip { space | Comment }

@local tokens {
  commentEnd { ClosingTag }
  commentNewLine { "\n" }
  @else CommentContent
}

@tokens {
  space { @whitespace+ }
  quoteSingle[@name=Quote] { "'" }
  quoteDouble[@name=Quote] { '"' }
  OpeningTag[closedBy=ClosingTag] { ScripletTag | WhitespaceSlurpingTag | OutputTag | OutputUnescapedTag | LiteralPercentTag }
  ClosingTag[openedBy=OpeningTag] { (PlainClosingTag | WhitespaceSlurpingClosingTag | TrimClosingTag) @eof? }
  // Opening tags
  ScripletTag { "<%" }
  WhitespaceSlurpingTag { ScripletTag "_" }
  OutputTag { ScripletTag "=" }
  OutputUnescapedTag { ScripletTag "-" }
  LiteralPercentTag { ScripletTag "%" }
  // Closing tags
  PlainClosingTag { "%>" }
  WhitespaceSlurpingClosingTag { "_" PlainClosingTag }
  TrimClosingTag { "-" PlainClosingTag }
}

@external tokens javascriptExpressionOrText from "./tokens" { JavascriptExpression, Text }

@detectDelim